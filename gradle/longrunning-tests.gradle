/*
 * Configuration of additional source set for long-running tests. Execute the 'longrunningTest'
 * task to run these tests.
 */

sourceSets {
	longrunning {
		java.srcDirs = ['longrunning/src']
		if (findProperty('compileXtend') == 'true') {
			xtendOutputDir = 'longrunning/xtend-gen'
		} else {
			java.srcDir 'longrunning/xtend-gen'
		}
		compileClasspath += sourceSets.test.output
		runtimeClasspath += sourceSets.test.output
	}
	suites {
		java.srcDirs =  ['suites', 'smoke-suites']
		compileClasspath += sourceSets.test.output + sourceSets.longrunning.output
		runtimeClasspath += sourceSets.test.output + sourceSets.longrunning.output
	}
}

configurations {
	longrunningCompile.extendsFrom testCompile
	longrunningRuntime.extendsFrom testRuntime
	suitesCompile.extendsFrom testCompile
	suitesRuntime.extendsFrom testRuntime
}

if (findProperty('compileXtend') == 'true') {
	generateLongrunningXtext.xtextClasspath = rootProject.configurations.getByName('xtendCompiler')
}

jar {
	from sourceSets.longrunning.output
	from sourceSets.suites.output
}

sourcesJar {
	from sourceSets.longrunning.allSource
	from sourceSets.suites.allSource
}

task longrunningTest(type: Test) {
	group = 'Verification'
	description = 'Runs the long-running unit tests.'
	testClassesDir = sourceSets.longrunning.output.classesDir
	classpath = sourceSets.longrunning.runtimeClasspath
}

test {
	exclude '**/*Suite.class'
}

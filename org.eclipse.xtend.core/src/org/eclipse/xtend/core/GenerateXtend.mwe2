/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtend.core.GenerateXtend

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*
import org.eclipse.xtext.generator.parser.antlr.*
import org.eclipse.xtext.resource.XtextResourceSet 

var rootPath = ".."
var baseName = "org.eclipse.xtend"
var runtimeProjectName = "org.eclipse.xtend.core"
var projectPath = "${rootPath}/${runtimeProjectName}"

var file.extensions = "xtend"

var lineDelimiter = "\n"
var backtrack = false
var memoize = false
var skipUnusedRules = true
var stripAllComments = true

var fileHeader = "/*******************************************************************************
 * Copyright (c) 2010-\${year} itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/"

Workflow {

	component = parser.JFlexLoader : jFlexMain {}

	component = XtextGenerator auto-inject {
		configuration = parser.XtendGeneratorModule {
			project = StandardProjectConfig auto-inject {
				runtime = {
					name = runtimeProjectName
				}
				eclipsePlugin = {
					enabled = true
					name = "org.eclipse.xtend.ide"
				}
				genericIde = {
					enabled = true
					name = "org.eclipse.xtend.ide.common"
				}
				ideaPlugin = {
					enabled = true
					name = "org.eclipse.xtend.idea" 
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "ISO-8859-1"
				lineDelimiter = lineDelimiter
				fileHeader = fileHeader
				preferXtendStubs = true
			}
		}
		language = {
			resourceSet = XtextResourceSet : resourceSet {}
			referencedResource = "platform:/resource/${runtimeProjectName}/model/RichStrings.genmodel"
			referencedResource = "platform:/resource/${runtimeProjectName}/model/Xtend.genmodel"
			name = "org.eclipse.xtend.core.Xtend"
			fileExtensions = file.extensions
			guiceModule = parser.XtendLanguageGeneratorModule {}
			
			fragment = grammarAccess.GrammarAccessFragment2 auto-inject {}
			fragment = serializer.SerializerFragment2 {
				generateXtendStub = false
				// generateDebugData = true
			}
			fragment = resourceFactory.ResourceFactoryFragment2 {}
			fragment = parser.antlr.XtextAntlrGeneratorFragment2 auto-inject {
				removeBacktrackingGuards = true
				lookaheadThreshold = '5'
				partialParsing = true
				debugGrammar = true
				options = auto-inject {}
			}
			fragment = parser.antlr.XtextAntlrGeneratorComparisonFragment auto-inject {
				registerAdvice = "org::eclipse::xtend::core::parser::AntlrGrammarWithTokens"
				registerAdvice = "org::eclipse::xtend::core::parser::ContentAssistAntlrGrammarWithTokens"
			}
			fragment = validation.ValidatorFragment2 {
				generateXtendStub = false
			}
			fragment = formatting.Formatter2Fragment2 {}
			fragment = generator.GeneratorFragment2 {}
			fragment = scoping.ImportNamespacesScopingFragment2 {
				generateXtendStub = false
			}
			fragment = builder.BuilderIntegrationFragment2 {}

			fragment = ui.quickfix.QuickfixProviderFragment2 {
				generateXtendStub = false
			}

			fragment = ui.labeling.LabelProviderFragment2 {
				generateStub = false
			}

			fragment = ui.outline.OutlineTreeProviderFragment2 {
				generateXtendStub = false
			}

			fragment = ui.outline.QuickOutlineFragment2 {}

			fragment = ui.compare.CompareFragment2 {}

			fragment = ui.contentAssist.ContentAssistFragment2 {
				generateXtendStub = false
			}

			fragment = ui.refactoring.RefactorElementNameFragment2 auto-inject {
				useJdtRefactoring = true
			}
			fragment = types.TypesGeneratorFragment2 {}
			fragment = xbase.XbaseGeneratorFragment2 {
				generateXtendInferrer = false
			}

			fragment = ui.templates.CodetemplatesGeneratorFragment2 {}

			fragment = adapter.FragmentAdapter {
				fragment = parser.JFlexGeneratorFragment auto-inject {
					patternPath = "/org/eclipse/xtend/core/parser/antlr/internal/patterns.incl"
					additionalRulePath = "/org/eclipse/xtend/core/parser/antlr/internal/rules.incl"
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = parser.JFlexGeneratorFragment auto-inject {
					ui = true
					patternPath = "/org/eclipse/xtend/core/parser/antlr/internal/patterns.incl"
					additionalRulePath = "/org/eclipse/xtend/core/parser/antlr/internal/rules.incl"
				}
			}
			fragment = org.eclipse.xtext.xtext.generator.idea.parser.antlr.XtextAntlrIDEAGeneratorFragment auto-inject {
				options = auto-inject {}
			}
			fragment = org.eclipse.xtext.xtext.generator.idea.IdeaPluginGenerator auto-inject {
				generateXtendStub = true
			}
		}
	}
	
	component = DirectoryCleaner {
		directory = "${projectPath}/emf-gen"
	}
	
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator auto-inject {
		genModel = "platform:/resource/${runtimeProjectName}/model/Xtend.genmodel"
		srcPath ="platform:/resource/${runtimeProjectName}/src"
		srcPath ="platform:/resource/org.eclipse.xtext.common.types/src"
		srcPath ="platform:/resource/org.eclipse.xtext.xbase/src"
	}
	
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator auto-inject {
		genModel = "platform:/resource/${runtimeProjectName}/model/RichStrings.genmodel"
		srcPath ="platform:/resource/${runtimeProjectName}/src"
		srcPath ="platform:/resource/org.eclipse.xtext.common.types/src"
		srcPath ="platform:/resource/org.eclipse.xtext.xbase/src"
	}
}
